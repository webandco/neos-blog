prototype(Webandco.Blog:DocumentPost) < prototype(Neos.Neos:Page) {
    bodyTag {
        attributes.class = 'post'
    }

    body {
        content = Webandco.Blog:DocumentPost.Renderer
    }
}

prototype(Webandco.Blog:DocumentPost.Renderer) < prototype(Neos.Fusion:Array) {
    articleStart = '<article>'
    articleStart.@position = 'after header'

    image = Webandco.Blog:Image {
        @position = 'after articleStart'
        @if.render = ${q(node).property('showImage') == null || q(node).property('showImage')}
    }

    blogHeader = Neos.Fusion:Tag {
        @position = 'after image'
        tagName = 'header'
        content = Neos.Fusion:Array {
            headline = Neos.Fusion:Tag {
                tagName = 'h1'
                content = ${q(node).property('headline') != null ? q(node).property('headline') : String.stripTags(q(node).children('title').property('title'))}
                @process.makeEditable = Neos.Neos:ContentElementEditable
                @process.makeEditable.property = ${'headline'}
            }

            lead = Neos.Fusion:Tag {
                tagName = 'p'
                content = ${q(node).property('lead') != null ? q(node).property('lead') : q(node).children('lead').property('text')}
                @process.makeEditable = Neos.Neos:ContentElementEditable
                @process.makeEditable.property = ${'lead'}
            }

            meta = Neos.Fusion:Template {
                templatePath = 'resource://Webandco.Blog/Private/Templates/FusionObjects/Meta.html'
                author = ${q(node).property('author')}
                datePublished = ${q(node).property('datePublished')}
            }
        }
    }

    main {
        @position = 'after blogHeader'
    }

    taxonomies = Neos.Fusion:Tag {
        @position = 'after main'
        tagName = 'aside'
        attributes.class = 'container'
        content = Neos.Fusion:Array {
            hr = Neos.Fusion:Tag {
                tagName = 'hr'
            }
            items = Neos.Fusion:Collection {
                collection = ${q(node).property('taxonomies')}
                itemRenderer = Neos.Fusion:Tag {
                    tagName = 'a'
                    attributes.class = 'btn btn-dark'
                    attributes.href = Neos.Neos:NodeUri {
                        node = ${item}
                        @process.convertUris = Neos.Neos:ConvertUris {
                            forceConversion = true
                        }
                    }
                    content = ${item.properties.title}
                }
            }
        }
    }

    articleEnd = '</article>'
    articleEnd.@position = 'after taxonomies'

    @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}

prototype(Webandco.Blog:DocumentPost.Short) < prototype(Neos.Fusion:Tag) {
    tagName = 'div'
    attributes.class = 'post-short'
    content = Webandco.Blog:DocumentPost.Renderer.Default
}

prototype(Webandco.Blog:DocumentPost.Renderer.Default) < prototype(Neos.Fusion:Array) {
    image = Webandco.Blog:Image
    content = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = 'content'
        content = Neos.Fusion:Array {
            headline = Neos.Fusion:Tag {
                tagName = 'h2'
                content = ${q(node).children('title').get(0) != null ? q(node).children('title').get(0).properties.title : q(node).property('title')}
                content.@process.clean = ${String.stripTags(value)}
            }
            lead = Neos.Fusion:Tag {
                tagName = 'p'
                content = ${q(node).children('lead').get(0) != null ? q(node).children('lead').get(0).properties.tex : q(node).property('lead')}
                content.@process.clean = ${String.stripTags(value)}
            }
            link = Neos.Fusion:Tag {
                tagName = 'a'
                attributes.class = 'btn btn-dark'
                attributes.href = Neos.Neos:NodeUri {
                    node = ${node}
                    @process.convertUris = Neos.Neos:ConvertUris {
                        forceConversion = true
                    }
                }
                content = 'mehr...'
            }
            @process.wrap = ${'<div class="content">' + value + '</div>'}
        }
    }
}

prototype(Webandco.Blog:DocumentPost.Renderer.Minimal) < prototype(Neos.Fusion:Array) {
    image = Webandco.Blog:Image
    content = Neos.Fusion:Tag {
        tagName = 'div'
        attributes.class = 'content'
        content = Neos.Fusion:Array {
            link = Neos.Fusion:Tag {
                tagName = 'a'
                attributes.href = Neos.Neos:NodeUri {
                    node = ${node}
                    @process.convertUris = Neos.Neos:ConvertUris {
                        forceConversion = true
                    }
                }
                content = ${q(node).children('title').get(0) != null ? q(node).children('title').get(0).properties.title : q(node).property('title')}
                content.@process.clean = ${String.stripTags(value)}
            }
            @process.wrap = ${'<div class="content">' + value + '</div>'}
        }
    }
}

prototype(Webandco.Blog:DocumentPostTeaser) < prototype(Neos.Fusion:Tag) {
    tagName = 'div'
    attributes.class = 'post-teaser'
    content = Neos.Fusion:Array {
        image = Webandco.Blog:Image
        link = Neos.Fusion:Tag {
            tagName = 'a'
            attributes.href = Neos.Neos:NodeUri {
                node = ${node}
                @process.convertUris = Neos.Neos:ConvertUris {
                    forceConversion = true
                }
            }
            content = ${q(node).property('title')}
        }
    }
}

prototype(Webandco.Blog:Image) < prototype(Neos.Fusion:Tag) {
    tagName = 'img'
    attributes.class = 'img-responsive'
    attributes.src = Neos.Fusion:Case {
        asset {
            condition = ${Type.isObject(q(node).property('imageSrc'))}
            renderer = Neos.Neos:ImageUri {
                allowCropping = true
                allowUpScaling = true
                asset = ${q(node).property('imageSrc')}
            }
        }

        resource {
            condition = ${String.indexOf(q(node).property('imageSrc'), 'resource') >= 0}
            renderer = Neos.Fusion:ResourceUri {
                path = ${q(node).property('imageSrc')}
            }
        }

        fallback {
            condition = ${true}
            renderer = '/_Resources/Static/Packages/Neos.Neos/Images/dummy-image.svg'
        }
    }

    attributes.alt = ${q(node).property('imageAlt')}
}

prototype(Webandco.Blog:Author) < prototype(Neos.Fusion:Tag) {
    tagName = 'em'
    content = Neos.Fusion:Array {
        author = Neos.Fusion:Value {
            value = ${q(node).property('author')}
        }
        datePublished = Neos.Fusion:Value {
            value = ${' ver&ouml;ffentlicht am ' + Date.format(q(node).property('datePublished'), 'd.m.Y')}
        }
    }
}

